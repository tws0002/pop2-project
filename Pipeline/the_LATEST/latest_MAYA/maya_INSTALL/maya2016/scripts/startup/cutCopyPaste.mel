// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc pasteScene()
{

	/* Begin: MAYA-39777

	   Maya 2015 copy writes out to ascii or binary
	   depending on file format open:
	   paste expects matching file type:
	   therefore ascii copy paste to binary fails
	*/

	string $userTmpDir = `internalVar -userTmpDir`;
	string $clipBoardFiles;
	string $filePath;
	
	//Get the latest maya clipboard scene, whether ascii or binary
	if (`about -application` == "maya")
	{
		string $presentWorkingDir = `pwd`;
		chdir($userTmpDir);

		if (`about -linux` == 1)
		{
			$clipBoardFiles = `system("ls -t maya_sceneClipBoard.m*")`;
		}
		else if (`about -mac` == 1)
		{
			$clipBoardFiles = `system("ls -t maya_sceneClipBoard.m*")`;
		}
		else if (`about -windows` == 1)
		{
			$clipBoardFiles = `system("dir /b /o:-D maya_sceneClipBoard.m*")`;
		}

		string $mayaSceneBuffer[];
		int $sceneCount = tokenize($clipBoardFiles, $mayaSceneBuffer);

		if (`file -q -exists ($userTmpDir + $mayaSceneBuffer[0])` == 1)
		{
			$filePath = ($userTmpDir + $mayaSceneBuffer[0]);
		}
		chdir($presentWorkingDir);
	}
	//End: MAYA-39777

	else
	{
		// Determine current scene file type
		string $getFileType[] = `file -q -type`;
		string $fileExt;

		/*
		if ( 0 != size($getFileType) && $getFileType[0] == "mayaAscii" ){
			$fileExt = ".ma";
		}
		else {
			$fileExt = ".mb";
		}
		*/

		if (`about -ltVersion`) {
			$fileExt = ".mlt";
		}
		else if (`about -evalVersion`) {
			$fileExt = ".mp";
		}

		// determine temp directory
		string $tmpFile = "/maya_sceneClipBoard" + $fileExt;
		//string $userTmpDir = `getenv TMPDIR`;
		$filePath = ($userTmpDir + $tmpFile);
	}

	// import scene
	string $newTransforms[] = `file -force
		-import
		-renameAll true
		-renamingPrefix "pasted_"
		-groupReference
		-returnNewNodes
		$filePath`;

	select -replace `ls -dag -head 1 $newTransforms`;
}

global proc cutCopyScene (int $cut){

    // Determine current Maya scenes file type
    string $getFileType[] = `file -q -type`;
    string $fileExt;
    string $fileType;

    if ( 0 != size($getFileType) && $getFileType[0] == "mayaAscii" ){
        $fileExt = ".ma";
        $fileType = "mayaAscii";
    }
    else {
        $fileExt = ".mb";
        $fileType = "mayaBinary";
    }
	if (`about -ltVersion`) {
		$fileExt  = ".mlt";
		$fileType = "mayaLT";
	}
	else if (`about -evalVersion`) {
		$fileExt  = ".mp";
		$fileType = "mayaPLE";
	}

	// determine temp directory
	string $tmpFile = "/maya_sceneClipBoard" + $fileExt;
	string $tmpdir = `getenv TMPDIR`;
	string $filePath = ($tmpdir + $tmpFile);

	// export selected nodes
	file -force
		-exportSelected
		-constructionHistory true
		-channels true
		-expressions true
		-constraints true
		-shader true
		-type $fileType
		$filePath;

	// delete nodes if user asks for cut
	if ($cut){
		delete;
	}
}

global proc cutCopyPaste (string $operation){

	// $operation:	cut
	//				copy
	//				paste

	// make sure generateChannelMenu has been sourced so channelBox commands work
	if (!`exists channelBoxCommand`){
		source generateChannelMenu.mel;
	}

	// based on active panel, channelBox selection and selected nodes
	// determine what user wants to cut copy paste

	string $option = "none";;
	string $parameter;
	string $selection[] = `ls -sl`;

	// determine if active panel is animation based
	// this value should override the channelBox cut copy paste
	string $currentPanel = `getPanel -underPointer`;
	if( $currentPanel == "" ) {
		$currentPanel = `getPanel -withFocus`;
	}
	string $panelType = `getPanel -typeOf $currentPanel`;
	if( $panelType == "scriptedPanel" ) {
		$panelType = `scriptedPanel -q -type $currentPanel`;
		if (   $panelType == "graphEditor"
			|| $panelType == "dopeSheetPanel"
			|| $panelType == "clipEditorPanel"
			|| $panelType == "cteEditor"
			)
		{
			$option = $panelType;
			if( $panelType == "dopeSheetPanel" ) {
				$parameter = $currentPanel + "OutlinerSelection";
			} else {
				$parameter = editorNameFromPanel( $currentPanel );
			}
		} else {
			$option = "nodes";
		}
	}
	// determine if anything is selected
	else if (size($selection)){
		$option = "nodes";

		// determine if attrs are selected in channelBox
		string $attrList[] = `channelBox -q -selectedMainAttributes mainChannelBox`;
		string $attrHistoryList[] = `channelBox -q -selectedHistoryAttributes mainChannelBox`;
		string $attrOutputList[] = `channelBox -q -selectedOutputAttributes mainChannelBox`;
		string $attrShapeList[] = `channelBox -q -selectedShapeAttributes mainChannelBox`;
		if ((size($attrList) != 0) ||
			(size($attrHistoryList) != 0) ||
			(size($attrOutputList) != 0) ||
			(size($attrOutputList) != 0)){

			$option = "channels";
		}

	} else if (!size($selection) && $operation == "paste"){
		$option = "nodes";
	}

	switch ($option){
	case "channels":
		if ($operation == "cut"){
			channelBoxCommand -cut;
		} else if ($operation == "copy"){
			channelBoxCommand -copy;
		} else {
			channelBoxCommand -paste;
		}
		break;

	case "graphEditor":
		if ($operation == "cut"){
			performCutKeyArgList 1 {"3", $parameter, "1"};
		} else if ($operation == "copy"){
			performCopyKeyArgList 1 {"3", $parameter, "1"};
		} else {
			performPasteKeyArgList 1 {"3", $parameter, "1"};
		}
		break;

	case "dopeSheetPanel":
		if ($operation == "cut"){
			performCutKeyArgList 1 {"0", $parameter, "2"};
		} else if ($operation == "copy"){
			performCopyKeyArgList 1 {"0", $parameter, "2"};
		} else {
			performPasteKeyArgList 1 {"0", $parameter, "2"};
		}
		break;

	case "clipEditorPanel":
		if ($operation == "cut"){
			doCutClipArgList 1 { $parameter };
		} else if ($operation == "copy"){
			doCopyClipArgList 1 { $parameter };
		} else {
			performPasteClip 0;
		}
		break;

	case "cteEditor":
		{
			// for basic copy & paste support, do nothing here to avoid wrong scene object duplication
			// the code to finish real clip copy & paste job locates inside TtimeviewView.cpp for now
		}
		break;

	case "nodes":
		if ($operation == "cut"){
			cutCopyScene 1;
		} else if ($operation == "copy"){
			cutCopyScene 0;
		} else {
			pasteScene;
		}
		break;

	case "none":
		// nothing selected - nothing required but a warning
		warning( (uiRes("m_cutCopyPaste.kNothingIsSelected")) );
		break;

	}


}
