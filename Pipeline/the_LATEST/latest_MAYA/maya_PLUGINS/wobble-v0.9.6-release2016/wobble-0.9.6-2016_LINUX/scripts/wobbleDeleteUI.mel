/*
	Copyright (C) 2006-2015 Felix Ulber

    version 1.2
    last change 27.09.2015
*/

proc deleteWobbleCreateMenu () {
	deleteUI -menuItem wobbleItem;
    deleteUI -menuItem wobbleDialogItem;
}

proc deleteWobblePaintWeightsMenu () {
    deleteUI -menuItem wobblePaintItem;
    deleteUI -menuItem wobblePaintDialogItem;
}

proc deleteWobbleFromExtraMenu () {
    
    if (`menu -q -ex feToolsMenu`) {
        setParent -m feToolsMenu;
        catchQuiet(deleteWobbleCreateMenu());
        catchQuiet(deleteWobblePaintWeightsMenu());

        string $myMenuItems[] = `menu -query -itemArray feToolsMenu`;
        if (size($myMenuItems) == 0)
            deleteUI -m feToolsMenu;
    }
}

proc deleteWobbleMayaPre2016Menu() {

	global string $gMainDeformationsMenu;
    global string $gMainEditDeformationsMenu;

    deleteUI -menuItem wobbleDividerItem;
    if (`menu -exists $gMainDeformationsMenu`)
    {
        setParent -menu $gMainDeformationsMenu;
        deleteWobbleCreateMenu();
    }
    if (`menu -exists $gMainEditDeformationsMenu`)
    {
        setParent -menu $gMainEditDeformationsMenu;
        deleteWobblePaintWeightsMenu();
    }
    
    deleteWobbleFromExtraMenu();
}

proc deleteWobbleMayaNewMenu() {

	global string $gMainDeformMenu;
	global string $gRigDeformationsMenu;
    
    if (`menu -exists $gMainDeformMenu`) {
        setParent -menu $gMainDeformMenu;
        deleteUI -menuItem wobbleDividerItem;
        deleteWobbleCreateMenu();
        deleteWobblePaintWeightsMenu();
    }
    
    if (`menu -exists $gRigDeformationsMenu`) {
        setParent -menu $gRigDeformationsMenu;
        deleteUI -menuItem wobbleDividerItem;
        deleteWobbleCreateMenu();
        deleteWobblePaintWeightsMenu();
    }
    
    deleteWobbleFromExtraMenu();
}

global proc wobbleDeleteUI()
{
	string $versionString = `about -v`;
	int $version = (int)$versionString;
	
	if ($version < 2016) {
		deleteWobbleMayaPre2016Menu();
	} else {
		deleteWobbleMayaNewMenu();
	}
}